apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
//apply from: "$project.rootDir/tools/script-findbugs.gradle"
//apply from: "$project.rootDir/tools/script-pmd.gradle"

Properties ownProperties = new Properties()
ownProperties.load(project.file('gradle.properties').newDataInputStream())
Properties envProperties = new Properties()
envProperties.load(project.rootProject.file("env.properties").newDataInputStream())

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.discovr.discord"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode ownProperties['VERSION_CODE'].toInteger()
        versionName ownProperties.getProperty('VERSION_NAME')
        resConfigs "en", "es"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            keyAlias envProperties.getProperty('KEY_ALIAS_DEBUG')
            keyPassword envProperties.getProperty('KEY_PASSWORD_DEBUG')
            storePassword envProperties.getProperty('STORE_PASSWORD_DEBUG')
            storeFile file(envProperties.getProperty('STORE_FILE_DEBUG'))
        }
        release {
            keyAlias envProperties.getProperty('KEY_ALIAS_RELEASE')
            keyPassword envProperties.getProperty('KEY_PASSWORD_RELEASE')
            storePassword envProperties.getProperty('STORE_PASSWORD_RELEASE')
            storeFile file(envProperties.getProperty('STORE_FILE_RELEASE'))
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            applicationIdSuffix ".release"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/rxjava.properties'
    }

    dexOptions {
        preDexLibraries true
        maxProcessCount 8
        javaMaxHeapSize "2g"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    List<String> activities = ['splash', 'main']
    sourceSets {
        main {
            activities.each { activity -> res.srcDirs "src/main/res-screen/${activity}" }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:27.0.2'
    compile 'com.android.support:design:27.0.2'
    compile 'com.android.support:cardview-v7:27.0.2'

    compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true
    }

    compile 'android.arch.persistence.room:runtime:1.0.0'
    annotationProcessor 'android.arch.persistence.room:compiler:1.0.0'

    compile 'com.google.dagger:dagger-android-support:2.11'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'

    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.0'

    // TODO used in the future
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    compile 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'

    compile 'com.fasterxml.jackson.core:jackson-core:2.8.10'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.10'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.8.7'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.8.7'

    compile 'com.daprlabs.aaron:cardstack:0.3.1'

    testCompile 'junit:junit:4.12'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs'
    })
}